cmake_minimum_required(VERSION 3.9.0 FATAL_ERROR)

include(cmake/option.cmake)

set(customization "default" CACHE STRING "Product customization")
nx_option(beta "Is this build is beta build" ON)
set(buildNumber "0" CACHE STRING "Build number")

nx_option(developerBuild
    "Disable this option for production builds (Jenkins)"
    ON)

if(developerBuild)
    set(default_cmake_build_type "Debug")
else()
    set(default_cmake_build_type "Release")
endif()

set(CMAKE_BUILD_TYPE ${default_cmake_build_type} CACHE STRING
    "One of [Debug Release RelWithDebInfo MinSizeRel].")

set(targetDevice "" CACHE STRING "Target device. For the list see cmake/toolchains.")
set(box "none")

if(targetDevice STREQUAL "none")
    set(targetDevice "")
endif()

# Early loading rdep to download a toolchain if needed.
include(cmake/find_python.cmake)
include(cmake/rdep.cmake)
nx_rdep_configure()

if(targetDevice AND NOT targetDevice STREQUAL "ios")
    set(CMAKE_TOOLCHAIN_FILE "${CMAKE_CURRENT_LIST_DIR}/cmake/toolchain/${targetDevice}.cmake")
    include(cmake/sync_toolchain.cmake)
endif()

project(vms VERSION 4.0.0)

if(targetDevice STREQUAL "ios")
    include(cmake/ios.cmake)
endif()

set(releaseVersion ${PROJECT_VERSION})
set(releaseVersion.short ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR})
set(releaseVersion.full ${releaseVersion}.${buildNumber})

set(mobileClientVersion 17.3)
set(mobileClientVersion.full ${mobileClientVersion}.${buildNumber})

set(CMAKE_INCLUDE_CURRENT_DIR ON)

set(CMAKE_DEBUG_POSTFIX "" CACHE INTERNAL "" FORCE)

list(APPEND CMAKE_MODULE_PATH
    ${PROJECT_SOURCE_DIR}/cmake
)

include(CMakeParseArguments)
include(CMakeDependentOption)
include(ExternalProject)
include(target_detection)
include(parameters_validation)
include(utils)
include(find_sources)
include(moc_utils)
include(qrc_utils)
include(precompiled_header)
include(strip_target)
include(mercurial)
include(ec_protocol)

set(customization_dir "${PROJECT_SOURCE_DIR}/customization/${customization}")

include(customization/default-values.cmake)
include(${customization_dir}/customization.cmake)
include(properties)
include(cloud_host)
include(vms_parts)
include(compiler_options)
include(output_directories)
include(distribution_names)

if(MACOSX OR IOS)
    include(macosx_keychain)
endif()

if(WINDOWS OR ANDROID OR IOS)
    set(BUILD_SHARED_LIBS OFF CACHE INTERNAL "")
else()
    set(BUILD_SHARED_LIBS ON CACHE INTERNAL "")
endif()

include(dependencies)
include(maven_compatibility)
include(generate_current_config)

if(WINDOWS)
    include(windows_signing)
endif()
include(project_utils)
include(test_utils)

nx_create_dev_qt_conf()

cmake_dependent_option(codeSigning "Enable code signing"
    ON "NOT developerBuild"
    OFF
)

list(APPEND CMAKE_PREFIX_PATH ${QT_DIR})
if(MACOSX)
    set(CMAKE_FRAMEWORK_PATH "${QT_DIR}/lib")
endif()
if(LINUX)
    set(CMAKE_EXE_LINKER_FLAGS
        "${CMAKE_EXE_LINKER_FLAGS} -Wl,-rpath-link,${QT_DIR}/lib")
    set(CMAKE_SHARED_LINKER_FLAGS
        "${CMAKE_SHARED_LINKER_FLAGS} -Wl,-rpath-link,${QT_DIR}/lib")
    set(CMAKE_EXE_LINKER_FLAGS
        "${CMAKE_EXE_LINKER_FLAGS} -Wl,-rpath-link,${CMAKE_LIBRARY_OUTPUT_DIRECTORY}")
    set(CMAKE_SHARED_LINKER_FLAGS
        "${CMAKE_SHARED_LINKER_FLAGS} -Wl,-rpath-link,${CMAKE_LIBRARY_OUTPUT_DIRECTORY}")
endif()

if(ANDROID)
    include(android-apk)
elseif(IOS)
    include(ios_app)
endif()

link_directories("${CMAKE_LIBRARY_OUTPUT_DIRECTORY}")

find_package(Qt5
    COMPONENTS
        LinguistTools
        Core
        Gui
        Network
        Xml
        XmlPatterns
        Sql
        Concurrent
        Multimedia)

if(targetDevice STREQUAL "bpi")
    set_property(TARGET Qt5::Gui APPEND PROPERTY
        INTERFACE_LINK_LIBRARIES EGL GLESv2 UMP)
endif()

if(withDesktopClient OR withMobileClient OR withNxTool)
    find_package(Qt5
        COMPONENTS
            Qml
            Quick)
endif()

if(withDesktopClient OR withNxTool)
    find_package(Qt5
        COMPONENTS
            Widgets
    )
endif()

set(QML_IMPORT_PATH
    "${QT_DIR}/qml"
    "${CMAKE_CURRENT_LIST_DIR}/client/nx_client_desktop/static-resources/qml"
    "${CMAKE_CURRENT_LIST_DIR}/client/mobile_client/static-resources/qml"
    "${CMAKE_CURRENT_LIST_DIR}/nxtool/static-resources/src/qml"
    CACHE STRING "Extra QML import paths")

if(ANDROID)
    find_package(Qt5 COMPONENTS AndroidExtras)
endif()

if(WINDOWS)
    # Prepare a file to be included into each Visual Studio project.
    nx_configure_file(${PROJECT_SOURCE_DIR}/msvc.user.props ${CMAKE_BINARY_DIR})
endif()

add_subdirectory(dependencies)

if (withMediaServer OR withTestCamera OR withTrayTool OR withDesktopClient OR withMobileClient OR withClouds)
    add_subdirectory(common_libs)
    add_subdirectory(common)
    add_subdirectory(appserver2)
    add_subdirectory(nx_cloud)
endif()

if(withMediaServer)
    add_subdirectory(mediaserver_core)
    add_subdirectory(mediaserver)
    add_subdirectory(plugins)
    add_subdirectory(storage_plugins)

    # TODO: Combine all SDKs (nx_storage_sdk, nx_sdk, nx_metadata_sdk) into a single sdk like this.
    add_subdirectory(nx_metadata_sdk)

    if(LINUX AND box MATCHES "none|tx1")
        add_subdirectory(root_tool)
    endif()
endif()

#add_subdirectory(_axis_controller)
#add_subdirectory(_vca_controller)

if(withTestCamera)
    add_subdirectory(testcamera)
endif()

if(withTrayTool)
    add_subdirectory(traytool)
endif()

if(withDesktopClient OR withMobileClient)
    add_subdirectory(client)
endif()

if(withNxTool)
    add_subdirectory(nxtool)
endif()

if(withTests)
    add_subdirectory(unit_tests)
    add_subdirectory(tests)
endif()

if(withDistributions)
    if(WINDOWS)
        add_subdirectory(wixsetup)
    endif()

    if(LINUX AND box STREQUAL "none") #< Desktop Linux.
        add_subdirectory(debsetup)
    endif()

    if(MACOSX)
        add_subdirectory(macsetup)
    endif()

    if(targetDevice MATCHES "bpi|bananapi|rpi|edge1")
        add_subdirectory(edge_firmware)
    endif()
endif()

include(fix_qt_vars)

if(beta)
    set(beta_bool "true")
else()
    set(beta_bool "false")
endif()
nx_configure_file(
    build_variables/maven/filter-resources/build_info.txt
    ${CMAKE_CURRENT_BINARY_DIR})

nx_save_known_files()
