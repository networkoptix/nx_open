set(depends
    mediaserver
    # The line below will be uncommented when all root_tool related tasks are finished.
    # root_tool
    server_plugins
    testcamera
)

# Variables used for configuring files.
set(common_depends
    "zlib1g (>= 1:1.1.4)"
    "debconf (>= 0.5) | debconf-2.0"
    "python"
    "psmisc"
    "systemd | upstart (>= 0.6.5-8)"
    "libsm6"
    "libice6"
    "libaudio2"
    "libogg0"
    "libxfixes3"
    "libxrender1"
    "libfontconfig1"
    "libgl1-mesa-glx"
    "cifs-utils"
)
string(REPLACE ";" ", " common.depends "${common_depends}") #< Make a comma-separated list.
set(arch.depends ", syslinux")

nx_configure_directory(
    ${CMAKE_CURRENT_SOURCE_DIR}/deb_files.in
    ${CMAKE_CURRENT_BINARY_DIR}
    @ONLY
    OUTPUT_FILES_VARIABLE configured_files)

nx_configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/build_distribution.conf.in
    ${CMAKE_CURRENT_BINARY_DIR}/build_distribution.conf
    @ONLY)
list(APPEND configured_files ${CMAKE_CURRENT_BINARY_DIR}/build_distribution.conf)

nx_create_qt_conf(${CMAKE_CURRENT_BINARY_DIR}/qt.conf)

set(zip_file ${distribution_output_dir}/${artifact.name.server_update}.zip)
set(deb_file ${distribution_output_dir}/${artifact.name.server}.deb)
set(installer_output_list ${zip_file} ${deb_file})

set(script ${CMAKE_CURRENT_SOURCE_DIR}/build_distribution.sh)
add_custom_command(
    OUTPUT ${installer_output_list}
    COMMAND ${script}
    DEPENDS ${script} ${depends} ${configured_files}
    COMMENT "Creating mediaserver .deb package"
)

add_custom_target(distribution_deb_mediaserver ALL
    DEPENDS ${installer_output_list}
)
