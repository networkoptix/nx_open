set(cloudGroup "test" CACHE STRING "Cloud instance group (dev, test, demo, prod)")
set(customCloudHost "" CACHE STRING "Cloud host. Leave empty to fetch cloud host from server.")

function(fetch_cloud_host cloudGroup customization result_variable)
    set(url "http://ireg.hdw.mx/api/v1/cloudhostfinder/?group=${cloudGroup}&vms_customization=${customization}")
    message(STATUS "Fetching cloud host from ${url}")
    set(cloud_tmp_file "${CMAKE_CURRENT_BINARY_DIR}/cloud_host.tmp")
    file(DOWNLOAD ${url} ${cloud_tmp_file} STATUS status)
    file(READ ${cloud_tmp_file} cloudHost)
    file(REMOVE ${cloud_tmp_file})
    if("${cloudHost}" STREQUAL "")
        message(FATAL_ERROR "Could not fetch cloud host from the server.\nStatus: ${status}")
    endif()
    set(${result_variable} ${cloudHost} PARENT_SCOPE)
endfunction()

if("${customCloudHost}" STREQUAL "")
    fetch_cloud_host(${cloudGroup} ${customization} cloudHost)

    set(compatibleCloudHosts)
    foreach(_customization ${compatibleCustomizations})
        fetch_cloud_host(${cloudGroup} ${_customization} _cloudHost)
        list(APPEND compatibleCloudHosts ${_cloudHost})
    endforeach()

    unset(_customization)
    unset(_cloudHost)
else()
    message(STATUS "Using the provided cloud host ${customCloudHost}")
    set(cloudHost ${customCloudHost})
endif()
