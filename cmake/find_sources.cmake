function(pick_file file_name result_name)
    if(file_name MATCHES "StdAfx.cpp" OR file_name MATCHES "StdAfx.h")
        set(${result_name} FALSE PARENT_SCOPE)
        return()
    elseif(file_name MATCHES "_win[./]")
        if(NOT WINDOWS)
            set(${result_name} FALSE PARENT_SCOPE)
            return()
        endif()
    elseif(file_name MATCHES "_unix[./]")
        if(NOT UNIX)
            set(${result_name} FALSE PARENT_SCOPE)
            return()
        endif()
    elseif(file_name MATCHES "_linux[./]")
        if(NOT LINUX)
            set(${result_name} FALSE PARENT_SCOPE)
            return()
        endif()
    elseif(file_name MATCHES "_mac[./]")
        if(NOT MACOSX AND NOT IOS)
            set(${result_name} FALSE PARENT_SCOPE)
            return()
        endif()
    elseif(file_name MATCHES "_macx[./]")
        if(NOT MACOSX)
            set(${result_name} FALSE PARENT_SCOPE)
            return()
        endif()
    elseif(file_name MATCHES "_android[./]")
        if(NOT ANDROID)
            set(${result_name} FALSE PARENT_SCOPE)
            return()
        endif()
    elseif(file_name MATCHES "_ios[./]")
        if(NOT IOS)
            set(${result_name} FALSE PARENT_SCOPE)
            return()
        endif()
    endif()
    set(${result_name} TRUE PARENT_SCOPE)
endfunction()

function(find_files src_dir extension var)
    file(GLOB_RECURSE CODE_FILES ${src_dir}/*.${extension})

    get_filename_component(src_dir_absolute ${src_dir} ABSOLUTE)

    foreach(file_name ${CODE_FILES})
        file(RELATIVE_PATH rel_file_name ${src_dir_absolute} ${file_name})
        if(rel_file_name STREQUAL "")
            set(rel_file_name ${file_name})
        endif()
        pick_file(${rel_file_name} PICK)
        if(${PICK})
            list(APPEND RESULT_FILES ${file_name})
        endif()
    endforeach()

    set(${var} ${RESULT_FILES} PARENT_SCOPE)
endfunction()

function(find_sources src_dir src_var hpp_var)
    find_files(${src_dir} "cpp" cpp_files)
    set(${src_var} ${cpp_files} PARENT_SCOPE)

    find_files(${src_dir} "h" hpp_files)
    set(${hpp_var} ${hpp_files} PARENT_SCOPE)
endfunction()
