set(depends
    mediaserver
    server_plugins
)

if(targetDevice STREQUAL "bpi")
    list(APPEND depends "mobile_client")
endif()

set(tar_file ${distribution_output_dir}/${artifact.name.server}.tar.gz)
set(zip_file ${distribution_output_dir}/${artifact.name.server_update}.zip)
set(installer_output_list ${tar_file} ${zip_file})

# Set variables which may depend on current project and are used by create_arm_installer.conf.
set(project.build.directory ${CMAKE_CURRENT_BINARY_DIR})
set(build.configuration "") #< Used by create_arm_installer.sh as suffix to lib and bin dirs.

set(conf_file maven/filter-resources/create_arm_installer.conf)
nx_configure_file(
    ${conf_file}
    ${CMAKE_CURRENT_BINARY_DIR}
    @ONLY)
list(APPEND depends ${conf_file})

if(targetDevice STREQUAL "edge1")
    set(box_dir "isd")
else()
    set(box_dir "rpi")
endif()

nx_configure_directory(
    ${CMAKE_CURRENT_SOURCE_DIR}/${box_dir}/maven/filter-resources
    ${CMAKE_CURRENT_BINARY_DIR}
    @ONLY
    OUTPUT_FILES_VARIABLE filtered1)

nx_configure_directory(
    ${CMAKE_CURRENT_SOURCE_DIR}/${box_dir}/maven/${targetDevice}
    ${CMAKE_CURRENT_BINARY_DIR}
    @ONLY
    OUTPUT_FILES_VARIABLE filtered2)

set(script ${CMAKE_CURRENT_SOURCE_DIR}/maven/bin-resources/create_arm_installer.sh)

add_custom_command(
    OUTPUT ${installer_output_list}
    COMMAND ${script} --no-client
    DEPENDS ${script} ${depends} ${filtered1} ${filtered2}
    COMMENT "Creating ARM installer archives"
)

add_custom_target(arm-installer ALL
    DEPENDS ${installer_output_list}
)
